plugins {
    id 'eu.xenit.docker-compose' version '5.3.2' apply false
    id "be.vbgn.ci-detect" version "0.5.0"
    id "org.sonarqube" version "3.5.0.2730"
}

sonarqube {
    properties {
        property "sonar.projectKey", "xenit-eu_alfred-telemetry"
        property "sonar.organization", "xenit-eu"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

def copyPropertyValueIfExists(sourcePropertyName, targetPropertyName) {
    if (project.hasProperty(sourcePropertyName)) {
        project.ext[targetPropertyName] = project.property(sourcePropertyName)
    }
}

allprojects {
    group = 'eu.xenit.alfred.telemetry'
    version = '0.9.0'

    apply plugin: 'java'
    apply plugin: 'jacoco'

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled true
        }
    }
    test.finalizedBy jacocoTestReport
    rootProject.tasks.sonarqube.dependsOn jacocoTestReport

    boolean isRelease = ci.branch?.startsWith("release")
    if (!isRelease) {
        version += "-SNAPSHOT"
    }

    ext {
        micrometerVersion = '1.0.6'
        jvmExtrasVersion = '0.1.2'

        junitJupiterVersion = '5.4.2'
        mockitoVersion = '2.27.0'
        hamcrestVersion = '1.3'
        awaitilityVersion = '4.1.0'
        restAssuredVersion = '4.0.0'
    }

    // It is not possible to set properties with a dot via GitHub Actions env variables, therefore we introduce support
    // for a non-dotted-equivalent
    copyPropertyValueIfExists('alfresco_nexus_username', 'org.alfresco.maven.nexus.username')
    copyPropertyValueIfExists('alfresco_nexus_password', 'org.alfresco.maven.nexus.password')
    copyPropertyValueIfExists('xenit_docker_registry_url', 'eu.xenit.docker.registry.url')
    copyPropertyValueIfExists('xenit_docker_registry_username', 'eu.xenit.docker.registry.username')
    copyPropertyValueIfExists('xenit_docker_registry_password', 'eu.xenit.docker.registry.password')

    repositories {
        mavenCentral()
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/groups/public'
        }
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/groups/private'
            credentials {
                username project.property('org.alfresco.maven.nexus.username')
                password project.property('org.alfresco.maven.nexus.password')
            }
        }
    }

    apply from: "${rootProject.projectDir}/publish.gradle"
}
