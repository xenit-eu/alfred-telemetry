plugins {
    id 'eu.xenit.docker-compose' version '5.3.2' apply false
    id "be.vbgn.ci-detect" version "0.5.0"
    id "org.sonarqube" version "3.5.0.2730"
}

sonarqube {
    properties {
        property "sonar.projectKey", "xenit-eu_alfred-telemetry"
        property "sonar.organization", "xenit-eu"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}


allprojects {
    group = 'eu.xenit.alfred.telemetry'
    version = '0.10.1'

    apply plugin: 'java'
    apply plugin: 'jacoco'

    compileJava {
        options.release = 11
    }
    compileTestJava {
        options.release = 11
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled true
        }
    }
    test.finalizedBy jacocoTestReport
    rootProject.tasks.sonarqube.dependsOn jacocoTestReport

    boolean isRelease = ci.branch?.startsWith("release")
    if (!isRelease) {
        version += "-SNAPSHOT"
    }

    ext {
        micrometerVersion = '1.0.6'
        jvmExtrasVersion = '0.1.2'

        junitJupiterVersion = '5.4.2'
        mockitoVersion = '4.5.1'
        byteBuddyVersion = '1.14.6'
        hamcrestVersion = '1.3'
        awaitilityVersion = '4.1.0'
        restAssuredVersion = '5.3.0'
    }


    repositories {
        mavenCentral()
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/groups/public'
        }
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/groups/private'
            credentials {
                username project.property('org.alfresco.maven.nexus.username')
                password project.property('org.alfresco.maven.nexus.password')
            }
        }
    }

    apply from: "${rootProject.projectDir}/publish.gradle"
}
