plugins {
    id 'idea'
    id 'java'
    id 'eu.xenit.alfresco' version "1.1.0"
    id 'eu.xenit.amp' version "1.1.0"
}

description = "Alfred Telemetry Platform module which enables exposure of Metrics using Micrometer"

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

ext {
    // Compiling against AEE 6.1 because:
    // * Some MeterBinder implementations monitor Enterprise only components (e.g. Hazelcast caches)
    // * Alfresco's Micrometer integration, (MetricsController, MetricsReporter) classes only available as of 6.1.
    // Compatibility with 5.x should be covered with integration tests
    alfrescoVersion = '6.1.1.11'
}

dependencies {
    alfrescoProvided enforcedPlatform("org.alfresco:acs-packaging:${alfrescoVersion}")

    alfrescoProvided('org.alfresco:alfresco-repository')

    // 'alfresco-enterprise-repository' not transitive because it will try to download a bunch of unreachable artifacts
    alfrescoProvided('org.alfresco:alfresco-enterprise-repository') { transitive = false }
    alfrescoProvided('com.hazelcast:hazelcast:3.12')
    alfrescoProvided("io.micrometer:micrometer-core:${micrometerVersion}") {
        exclude group: "org.slf4j", module: "*"
    }
    alfrescoProvided("io.github.mweirauch:micrometer-jvm-extras:${jvmExtrasVersion}") {
        exclude group: "org.slf4j", module: "*"
    }
    alfrescoProvided 'javax.servlet:javax.servlet-api'
    compileOnly 'org.apache.tomcat:tomcat-catalina:7.0.92'
    alfrescoProvided "io.micrometer:micrometer-registry-graphite:${micrometerVersion}"
    alfrescoProvided "io.micrometer:micrometer-registry-jmx:${micrometerVersion}"
    alfrescoProvided "io.micrometer:micrometer-registry-prometheus:${micrometerVersion}"

    alfrescoProvided "commons-dbcp:commons-dbcp:1.4"
    alfrescoProvided "org.apache.commons:commons-dbcp2:2.9.0"
    alfrescoProvided "com.google.code.findbugs:jsr305:3.0.2"

    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testImplementation "org.awaitility:awaitility:${awaitilityVersion}"
    testImplementation "org.mockito:mockito-inline:${mockitoVersion}"
    testImplementation "net.bytebuddy:byte-buddy:${byteBuddyVersion}"
    testRuntimeOnly 'org.alfresco:alfresco-remote-api'
}

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
    systemProperty 'micrometerVersion', project.property("micrometerVersion")
}

processResources {
    filesMatching("**/module.properties") {
        expand project.properties
    }
}

sourceSets {
    main {
        amp {
            module("src/main/resources/alfresco/module/alfred-telemetry-platform/module.properties")
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.amp
        }
    }
}

targetCompatibility = JavaVersion.VERSION_11
