pluginManager.withPlugin('maven-publish') {
    apply plugin: 'signing'

    publishing {

        repositories {
            if ("${project.version}".endsWith('-SNAPSHOT')) {
                sonatypeSnapshots {
                    credentials {
                        username = project.findProperty('publish_username')
                        password = project.findProperty('publish_password')
                    }
                }
            } else {
                sonatypeMavenCentral {
                    credentials {
                        username = project.findProperty('publish_username')
                        password = project.findProperty('publish_password')
                    }
                }
            }
        }
        publications {
            all {
                pom {
                    url = 'https://github.com/xenit-eu/alfred-telemetry'
                    name = "alfred-telemetry"
                    description = project.description

                    scm {
                        connection = 'scm:git:git@github.com:xenit-eu/alfred-telemetry.git'
                        developerConnection = 'scm:git:git@github.com:xenit-eu/alfred-telemetry.git'
                        url = 'https://github.com/xenit-eu/alfred-telemetry.git'
                    }
                    developers {
                        developer {
                            id = "xenit"
                            name = "Xenit Solutions NV"
                        }
                    }

                    licenses {
                        license {
                            name = 'GNU Lesser General Public License v3.0'
                            url = 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                        }
                    }
                }
            }
        }
    }
}