plugins {
    id 'java'
    id 'idea'
}

ext {
    slf4jVersion = '1.7.25'
    extraDependencies = []
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testImplementation "io.rest-assured:rest-assured:${restAssuredVersion}"
    testImplementation "org.apache.httpcomponents:httpclient:4.5.12"

    testImplementation group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jVersion}"
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: "${slf4jVersion}"
}

test {
    // By default tests should only run in the subprojects. To manually run tests in your IDEA, uncomment:
    enabled = false
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

subprojects {
    apply plugin: 'java'

    def alfrescoVersion = project.name[-2..-1]
    def alfrescoEdition = "Community"
    if(project.name.contains("-enterprise-")) {
        alfrescoEdition = "Enterprise"
    }

    apply from: "${project.projectDir}/overload.gradle"

    description = "Alfresco ${alfrescoVersion} with Alfred Micrometer"

    task integrationTest(type: Test, group: 'verification') {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
        testClassesDirs = project.parent.sourceSets.test.output.classesDirs
        classpath = project.parent.sourceSets.test.runtimeClasspath
        outputs.upToDateWhen { false }
        doFirst {
            dockerCompose.solr.exposeAsSystemProperties(integrationTest)
            systemProperty 'solrFlavor', "${solrFlavor}"
            systemProperty 'alfrescoEdition', alfrescoEdition

        }
    }

    check.dependsOn integrationTest

    apply plugin: 'eu.xenit.docker-alfresco'
    apply plugin: 'eu.xenit.docker-compose.auto'

    dependencies {
        alfrescoAmp project(path: ":alfred-telemetry-platform", configuration: "amp")

        if (project.hasProperty("alfrescoBaseWarBom")) {
            baseAlfrescoWar platform("${alfrescoBaseWarBom}")
        }

        // load extra dependencies from overload.gradle
        alfrescoSM project.extraDependencies

        baseAlfrescoWar "${alfrescoBaseWar}"
    }

    dockerAlfresco {
        baseImage = "${alfrescoBaseImage}"
        leanImage = true

        dockerBuild {
            repository = "private.docker.xenit.eu/alfred-telemetry/alfresco-alfred-telemetry-${alfrescoVersion}"
            tags = ["${version}"]
            automaticTags = false
        }
    }

    afterEvaluate {
        graphiteComposeUp.dependsOn(buildDockerImage)
        prometheusComposeUp.dependsOn(buildDockerImage)
        buildDockerImage {
            doLast {
                dockerCompose.graphite.environment.put 'DOCKER_IMAGE', getImageId()
                dockerCompose.prometheus.environment.put 'DOCKER_IMAGE', getImageId()
                dockerCompose.solrPrometheus.environment.put 'DOCKER_IMAGE', getImageId()
            }
        }
    }

    // Make sure solr images project is loaded before we try to load a task from it.
    // Necessary because :alfred-telemetry-solr:alfred-telemetry-solr* is cross-configured from the :alfred-telemetry-solr project
    project.evaluationDependsOn(":alfred-telemetry-solr")
    project.evaluationDependsOn(":alfred-telemetry-solr:alfred-telemetry-${solrFlavor}")
    def solrImage = project(":alfred-telemetry-solr:alfred-telemetry-${solrFlavor}")

    dockerCompose {
        useComposeFiles = [
                '../src/test/resources/compose/docker-compose.yml',
                '../src/test/resources/compose/docker-compose-jmx.yml'
        ]

        removeVolumes = true
        captureContainersOutput = false

        // Uncomment for quick iterations when developing integration tests
//         stopContainers = false

        // expose alfresco/inflow on a random port, comment to use default port (8080)
//        environment.put 'ALFRESCO_TCP_8080', '8080'

        dev {
            useComposeFiles = [
                    '../src/test/resources/compose/docker-compose.yml',
                    '../src/test/resources/compose/docker-compose-development.yml'
            ]
        }

        graphite {
            useComposeFiles = [
                    '../src/test/resources/compose/docker-compose.yml',
                    '../src/test/resources/compose/docker-compose-graphite.yml',
                    '../src/test/resources/compose/docker-compose-grafana.yml',
            ]
        }

        prometheus {
            useComposeFiles = [
                    '../src/test/resources/compose/docker-compose.yml',
                    '../src/test/resources/compose/docker-compose-prometheus.yml',
                    '../src/test/resources/compose/docker-compose-grafana.yml'
            ]
        }

        sharding {
            useComposeFiles = [
                    '../src/test/resources/compose/docker-compose.yml',
                    '../src/test/resources/compose/docker-compose-sharding.yml'
            ]
        }

        solr {
            environment.put 'SOLR_ENDPOINT', ('solr4'.equals(solrFlavor) ? 'solr4' : 'solr')
            environment.put 'INDEX', solrFlavor
            environment.put 'ALFRED_TELEMETRY_EXPORT_GRAPHITE_ENABLED', 'false'

            useComposeFiles = [
                    '../src/test/resources/compose/docker-compose.yml',
                    '../src/test/resources/compose/docker-compose-solr.yml'
            ]
            isRequiredBy(integrationTest)
        }

        solrPrometheus {
            environment.put 'SOLR_ENDPOINT', ('solr4'.equals(solrFlavor) ? 'solr4' : 'solr')
            environment.put 'METRICS_TOMCAT_ENABLED', ('solr4'.equals(solrFlavor) ? 'true' : 'false')
            environment.put 'METRICS_JETTY_ENABLED', ('solr6'.equals(solrFlavor) ? 'true' : 'false')
            environment.put 'INDEX', solrFlavor
            environment.put 'ALFRED_TELEMETRY_EXPORT_GRAPHITE_ENABLED', 'false'
            if ("solr4".equals(solrFlavor)) {
                useComposeFiles = [
                        '../src/test/resources/compose/docker-compose.yml',
                        '../src/test/resources/compose/docker-compose-solr.yml',
                        '../src/test/resources/compose/docker-compose-prometheus-solr4.yml',
                        '../src/test/resources/compose/docker-compose-grafana.yml'
                ]
            } else if ("solr6".equals(solrFlavor)) {
                useComposeFiles = [
                        '../src/test/resources/compose/docker-compose.yml',
                        '../src/test/resources/compose/docker-compose-solr.yml',
                        '../src/test/resources/compose/docker-compose-prometheus-solr6.yml',
                        '../src/test/resources/compose/docker-compose-grafana.yml'
                ]
            }
        }

        solrGraphite {
            environment.put 'SOLR_ENDPOINT', ('solr4'.equals(solrFlavor) ? 'solr4' : 'solr')
            environment.put 'METRICS_TOMCAT_ENABLED', ('solr4'.equals(solrFlavor) ? 'true' : 'false')
            environment.put 'METRICS_JETTY_ENABLED', ('solr6'.equals(solrFlavor) ? 'true' : 'false')
            environment.put 'INDEX', solrFlavor
            environment.put 'ALFRED_TELEMETRY_EXPORT_GRAPHITE_ENABLED', 'true'
            environment.put 'ALFRED_TELEMETRY_EXPORT_GRAPHITE_HOST', 'carbon'
            useComposeFiles = [
                    '../src/test/resources/compose/docker-compose.yml',
                    '../src/test/resources/compose/docker-compose-solr.yml',
                    '../src/test/resources/compose/docker-compose-graphite.yml',
                    '../src/test/resources/compose/docker-compose-grafana.yml'
            ]
        }

        dockerCompose.solr.fromBuildImage("SOLR_DOCKER_IMAGE", solrImage.tasks.named("buildDockerImage"))
        dockerCompose.solrPrometheus.fromBuildImage("SOLR_DOCKER_IMAGE", solrImage.tasks.named("buildDockerImage"))
        dockerCompose.solrGraphite.fromBuildImage("SOLR_DOCKER_IMAGE", solrImage.tasks.named("buildDockerImage"))
    }
}
